buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0"
        classpath "org.ajoberstar:grgit:1.1.0"
    }
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "com.github.hierynomus.license"
    apply plugin: "java"

    ext {
        appName = "Undertailor"
        gdxVersion = '1.9.3'
        roboVMVersion = '2.1.0'
        box2DLightsVersion = '1.4'
        ashleyVersion = '1.7.0'
        aiVersion = '1.8.0'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }
    
    license {
        mapping {
            java = "PHP"
        }

        excludes([
            "assets/**",
            "mod/**",
            "**/package-info.java",
            "**/log4j.properties",
            "test/**"
        ])

        header rootProject.file("LICENSE")
    }
}

ext {
    git = org.ajoberstar.grgit.Grgit.open(file('.'))
    revision = git.head().abbreviatedId
    version = "1.0"

    finalVersion = "${version}-${revision}"
    mainClassName = "me.scarlet.undertailor.desktop.DesktopLauncher"
}

project(":desktop") {
    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
    }
}

project(":core") {
    dependencies {
        compile 'org.luaj:luaj-jse:3.0.1'
        compile "org.slf4j:slf4j-log4j12:1.7.21"
        compile 'ninja.leaping.configurate:configurate-json:3.0'

        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
    }
}

import org.apache.tools.ant.filters.ReplaceTokens

tasks.eclipse.doLast {
    delete ".project"
}

task run(type: JavaExec) {
    main = project.mainClassName
    classpath = project(":desktop").sourceSets.main.runtimeClasspath
    standardInput = System.in

    workingDir = new File("../test/")
    workingDir.mkdirs()
    ignoreExitValue = true
}

task dist(type: Jar) {
    from files(project(":core").sourceSets.main.output.classesDir)
    from files(project(":desktop").sourceSets.main.output.classesDir)
    from files(project(":core").sourceSets.main.output.resourcesDir)
    from { project(":desktop").configurations.compile.collect {zipTree(it)} }
    
    manifest {
        attributes 'Main-Class': project.mainClassName
    }

    baseName = "undertailor"
}

dist.dependsOn project(":core").licenseMain
dist.dependsOn project(":desktop").licenseMain
dist.dependsOn project(":core").classes
dist.dependsOn project(":desktop").classes
